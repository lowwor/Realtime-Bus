apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'jacoco-android'


def preDexEnabled = "true".equals(System.getProperty("pre_dex", "true"))
def isCi = "true".equals(System.getenv("CI"))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "com.lowwor.realtimebus"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode gitCommitCount()
        versionName "2.0.3"
        manifestPlaceholders = [BUGLY_APP_CHANNEL: "Unspecified"]
        // Make generated apks contain version name and release time.
        archivesBaseName = "RealTimeBus_v-$versionName-${releaseTime()}"
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi' ,  'armeabi-v7a' ,'x86', 'x86_64', 'arm64-v8a'
        }
        resValue "string", "version_name", "珠海公交 v"+versionName
    }
    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'

            // Enables code coverage with Jacoco only for instrumentation tests.
            // Coverage for JVM tests done by code_quality_tools/jacoco.gradle.
            testCoverageEnabled true
        }

        release {
            // Enables code coverage with Jacoco only for instrumentation tests.
            // Coverage for JVM tests done by code_quality_tools/jacoco.gradle.
            testCoverageEnabled true

            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }

    flavorDimensions 'env','channel'
    productFlavors {
        prod {
            dimension  'env'
        }
        mock {
            dimension  'env'
            applicationIdSuffix = ".mock"
        }
        fir {
            dimension  'channel'
            manifestPlaceholders = [BUGLY_APP_CHANNEL: "FIR.IM"]
        }
        wandoujia {
            dimension  'channel'
            manifestPlaceholders = [BUGLY_APP_CHANNEL: "WanDouJia"]
        }
        yingyongbao {
            dimension  'channel'
            manifestPlaceholders = [BUGLY_APP_CHANNEL: "YingYongBao"]
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
        //Remove flavor not needed in CI
        if (isCi) {
            if(variant.getFlavors().get(1).name.equals('fir')||variant.getFlavors().get(1).name.equals('wandoujia')) {
                variant.setIgnore(true);
            }
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
        javaMaxHeapSize "4g"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //parcel
    compile 'com.hannesdorfmann.parcelableplease:annotation:1.0.1'
    apt 'com.hannesdorfmann.parcelableplease:processor:1.0.1'
    //support
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    //Rx
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1'
    //Dagger
    apt 'com.google.dagger:dagger-compiler:2.0'
    compile 'com.google.dagger:dagger:2.0'
    compile 'org.glassfish:javax.annotation:10.0-b28'
    //Gson
    compile 'com.google.code.gson:gson:2.7'
    //Logger
    compile 'com.orhanobut:logger:1.11'
    //Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.1.1-SNAPSHOT'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:retrofit-mock:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    //Stetho
    compile 'com.facebook.stetho:stetho:1.1.0'
    //Leak Canary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    //RecyclerView Data Binding
    compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter:2.0.0-beta1'
    compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter-recyclerview:2.0.0-beta1'
    //Bugly
    compile 'com.tencent.bugly:crashreport_upgrade:1.1.7'
    //bottom navigation
    compile 'com.roughike:bottom-bar:1.3.3'
    compile 'com.ncapdevi:frag-nav:1.0.3'
    //test
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.google.truth:truth:0.28'
    testCompile 'org.robolectric:robolectric:3.0'
}

//获得git commit的数目
def gitCommitCount() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = stdout
        }
        def commitCount = stdout.toString().trim().toInteger()
        return commitCount
    }
    catch (ignored) {
        return 0;
    }
}

def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim().substring(0, 4)
}

//使用独立的signing.propreties保存签名信息
def File propFile = file('signing.properties');
if (isCi) {
    android.signingConfigs.release.storeFile = file('../lowwor.jks')
    android.signingConfigs.release.storePassword = System.getenv("STORE_PASSWORD")
    android.signingConfigs.release.keyAlias = System.getenv('KEY_ALIAS')
    android.signingConfigs.release.keyPassword = System.getenv('KEY_PASSWORD')
} else if (propFile.exists()) {
    def Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        def keystoreFile = file(props["STORE_FILE"])
        if (keystoreFile.canRead()) {
            android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
            android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            println("keystore file not found: " + props["KEYSTORE_FILE"])
            android.buildTypes.release.signingConfig = null
        }
    } else {
        println("signing.properties found but some entries missing")
        android.buildTypes.release.signingConfig = null
    }
} else {
    println("signing.properties not found: ")
    android.buildTypes.release.signingConfig = null
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}


